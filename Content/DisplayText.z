class DisplayText : ZilchComponent
{
    [Property]
    var Delay : Real = 0.02;
    var CurDelay : Real = 0;
    var Count : Integer = 0;
    var CurText : String = "";
    var ShowingBar : Boolean = true;
    var BarTimer : Real = 0.4;
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
    }

    function OnLogicUpdate(event : UpdateEvent)
    {
        var  goalText = this.GameSession.GameStats.CurrentString;
        //Subtract from current letter delay every frame.
        this.CurDelay -= event.Dt;
        if (this.GameSession.GameStats.AutoComplete)
        {
            this.CurText = goalText;
        }
        //When it's zero, incriment count, play sound, and display strings[count] upt the index count.
        if (this.CurDelay <= 0 && this.Count <= goalText.Count && this.CurText != goalText)
        {
            this.CurDelay = this.Delay;
            this.CurText = goalText.SubString(0, this.Count);
            this.Count += 1;
            //this.Owner.SimpleSound.Play();
        }
        
        if (this.GameSession.GameStats.AutoComplete
            || this.CurText == goalText
            || (Zero.Keyboard.KeyIsPressed(Keys.Enter) && this.Count > 2))
        {
            this.CurText = goalText;
            this.Owner.SoundEmitter.Stop();
            this.GameSession.GameStats.DoneTyping = true;
            this.Count = 0;
        }
        else
        {
            if (this.Owner.SoundEmitter.IsPlaying() == false)
            {
                var ran : Random = new Random();
                if (ran.DieRoll(2) == 1) this.Owner.SoundEmitter.PlayCue(SoundCue.TypingCue);
                else this.Owner.SoundEmitter.PlayCue(SoundCue.TypingSound2);
            }
            this.GameSession.GameStats.DoneTyping = false;
        }
        
        if (this.GameSession.GameStats.AutoComplete && this.ShowingBar)
        {
            this.Owner.SpriteText.Text = "`this.CurText`|";
        }
        else
        {
            this.Owner.SpriteText.Text = this.CurText;
        }
        this.BarTimer -= event.Dt;
        if (this.BarTimer <= 0)
        {
            this.BarTimer = 0.4;
            this.ShowingBar = !this.ShowingBar;
        }
    }
}
